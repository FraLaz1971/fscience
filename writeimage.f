C *************************************************************************
      SUBROUTINE WRITEIMAGE

C  CREATE A FITS PRIMARY ARRAY CONTAINING A 2-D IMAGE

      INTEGER STATUS,UNIT,BLOCKSIZE,BITPIX,NAXIS,NAXES(2)
      INTEGER I,J,GROUP,FPIXEL,NELEMENTS,ARRAY(300,200)
      CHARACTER FILENAME*80
      LOGICAL SIMPLE,EXTEND

C  THE STATUS PARAMETER MUST BE INITIALIZED BEFORE USING FITSIO.  A
C  POSITIVE VALUE OF STATUS IS RETURNED WHENEVER A SERIOUS ERROR OCCURS.
C  FITSIO USES AN `INHERITED STATUS' CONVENTION, WHICH MEANS THAT IF A
C  SUBROUTINE IS CALLED WITH A POSITIVE INPUT VALUE OF STATUS, THEN THE
C  SUBROUTINE WILL EXIT IMMEDIATELY, PRESERVING THE STATUS VALUE. FOR 
C  SIMPLICITY, THIS PROGRAM ONLY CHECKS THE STATUS VALUE AT THE END OF 
C  THE PROGRAM, BUT IT IS USUALLY BETTER PRACTICE TO CHECK THE STATUS 
C  VALUE MORE FREQUENTLY.

      STATUS=0

C  NAME OF THE FITS FILE TO BE CREATED:
      FILENAME='ATESTFILEZ.FITS'

C  DELETE THE FILE IF IT ALREADY EXISTS, SO WE CAN THEN RECREATE IT.
C  THE DELETEFILE SUBROUTINE IS LISTED AT THE END OF THIS FILE.
      CALL DELETEFILE(FILENAME,STATUS)

C  GET AN UNUSED LOGICAL UNIT NUMBER TO USE TO OPEN THE FITS FILE.
C  THIS ROUTINE IS NOT REQUIRED;  PROGRAMMERS CAN CHOOSE ANY UNUSED
C  UNIT NUMBER TO OPEN THE FILE.
      CALL FTGIOU(UNIT,STATUS)

C  CREATE THE NEW EMPTY FITS FILE.  THE BLOCKSIZE PARAMETER IS A
C  HISTORICAL ARTIFACT AND THE VALUE IS IGNORED BY FITSIO.
      BLOCKSIZE=1
      CALL FTINIT(UNIT,FILENAME,BLOCKSIZE,STATUS)

C  INITIALIZE PARAMETERS ABOUT THE FITS IMAGE.
C  BITPIX = 16 MEANS THAT THE IMAGE PIXELS WILL CONSIST OF 16-BIT
C  INTEGERS.  THE SIZE OF THE IMAGE IS GIVEN BY THE NAXES VALUES. 
C  THE EXTEND = TRUE PARAMETER INDICATES THAT THE FITS FILE
C  MAY CONTAIN EXTENSIONS FOLLOWING THE PRIMARY ARRAY.
      SIMPLE=.TRUE.
      BITPIX=16
      NAXIS=2
      NAXES(1)=300
      NAXES(2)=200
      EXTEND=.TRUE.

C  WRITE THE REQUIRED HEADER KEYWORDS TO THE FILE
      CALL FTPHPR(UNIT,SIMPLE,BITPIX,NAXIS,NAXES,0,1,EXTEND,STATUS)

C  INITIALIZE THE VALUES IN THE IMAGE WITH A LINEAR RAMP FUNCTION
      DO J=1,NAXES(2)
          DO I=1,NAXES(1)
              ARRAY(I,J)=I - 1 +J - 1
          END DO
      END DO

C  WRITE THE ARRAY TO THE FITS FILE.
C  THE LAST LETTER OF THE SUBROUTINE NAME DEFINES THE DATATYPE OF THE
C  ARRAY ARGUMENT; IN THIS CASE THE 'J' INDICATES THAT THE ARRAY HAS AN
C  INTEGER*4 DATATYPE. ('I' = I*2, 'E' = REAL*4, 'D' = REAL*8).
C  THE 2D ARRAY IS TREATED AS A SINGLE 1-D ARRAY WITH NAXIS1 * NAXIS2
C  TOTAL NUMBER OF PIXELS.  GROUP IS SELDOM USED PARAMETER THAT SHOULD
C  ALMOST ALWAYS BE SET = 1.
      GROUP=1
      FPIXEL=1
      NELEMENTS=NAXES(1)*NAXES(2)
      CALL FTPPRJ(UNIT,GROUP,FPIXEL,NELEMENTS,ARRAY,STATUS)

C  WRITE ANOTHER OPTIONAL KEYWORD TO THE HEADER
C  THE KEYWORD RECORD WILL LOOK LIKE THIS IN THE FITS FILE:
C
C  EXPOSURE=                 1500 / TOTAL EXPOSURE TIME
C
      CALL FTPKYJ(UNIT,'EXPOSURE',1500,'TOTAL EXPOSURE TIME',STATUS)

C  THE FITS FILE MUST ALWAYS BE CLOSED BEFORE EXITING THE PROGRAM. 
C  ANY UNIT NUMBERS ALLOCATED WITH FTGIOU MUST BE FREED WITH FTFIOU.
      CALL FTCLOS(UNIT, STATUS)
      CALL FTFIOU(UNIT, STATUS)

C  CHECK FOR ANY ERRORS, AND IF SO PRINT OUT ERROR MESSAGES.
C  THE PRINTERROR SUBROUTINE IS LISTED NEAR THE END OF THIS FILE.
      IF (STATUS .GT. 0)CALL PRINTERROR(STATUS)
      END
