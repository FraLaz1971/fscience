C *************************************************************************
      SUBROUTINE WRITEASCII

C  CREATE AN ASCII TABLE CONTAINING 3 COLUMNS AND 6 ROWS.  FOR CONVENIENCE,
C  THE ASCII TABLE EXTENSION IS APPENDED TO THE FITS IMAGE FILE CREATED 
C  PREVIOUSLY BY THE WRITEIMAGE SUBROUTINE.

      INTEGER STATUS,UNIT,READWRITE,BLOCKSIZE,TFIELDS,NROWS,ROWLEN
      INTEGER NSPACE,TBCOL(3),DIAMETER(6), COLNUM,FROW,FELEM
      REAL DENSITY(6)
      CHARACTER FILENAME*40,EXTNAME*16
      CHARACTER*16 TTYPE(3),TFORM(3),TUNIT(3),NAME(6)
      DATA TTYPE/'PLANET','DIAMETER','DENSITY'/
      DATA TFORM/'A8','I6','F4.2'/
      DATA TUNIT/' ','KM','G/CM'/
      DATA NAME/'MERCURY','VENUS','EARTH','MARS','JUPITER','SATURN'/
      DATA DIAMETER/4880,12112,12742,6800,143000,121000/
      DATA DENSITY/5.1,5.3,5.52,3.94,1.33,0.69/

C  THE STATUS PARAMETER MUST ALWAYS BE INITIALIZED.
      STATUS=0

C  NAME OF THE FITS FILE TO APPEND THE ASCII TABLE TO:
      FILENAME='ATESTFILEZ.FITS'

C  GET AN UNUSED LOGICAL UNIT NUMBER TO USE TO OPEN THE FITS FILE.
      CALL FTGIOU(UNIT,STATUS)

C  OPEN THE FITS FILE WITH WRITE ACCESS.
C  (READWRITE = 0 WOULD OPEN THE FILE WITH READONLY ACCESS).
      READWRITE=1
      CALL FTOPEN(UNIT,FILENAME,READWRITE,BLOCKSIZE,STATUS)

C  FTCRHD CREATES A NEW EMPTY FITS EXTENSION FOLLOWING THE CURRENT
C  EXTENSION AND MOVES TO IT.  IN THIS CASE, FITSIO WAS INITIALLY
C  POSITIONED ON THE PRIMARY ARRAY WHEN THE FITS FILE WAS FIRST OPENED, SO
C  FTCRHD APPENDS AN EMPTY EXTENSION AND MOVES TO IT.  ALL FUTURE FITSIO
C  CALLS THEN OPERATE ON THE NEW EXTENSION (WHICH WILL BE AN ASCII
C  TABLE).
      CALL FTCRHD(UNIT,STATUS)

C  DEFINE PARAMETERS FOR THE ASCII TABLE (SEE THE ABOVE DATA STATEMENTS)
      TFIELDS=3
      NROWS=6
      EXTNAME='PLANETS_ASCII'
      
C  FTGABC IS A CONVENIENT SUBROUTINE FOR CALCULATING THE TOTAL WIDTH OF
C  THE TABLE AND THE STARTING POSITION OF EACH COLUMN IN AN ASCII TABLE.
C  ANY NUMBER OF BLANK SPACES (INCLUDING ZERO)  MAY BE INSERTED BETWEEN
C  EACH COLUMN OF THE TABLE, AS SPECIFIED BY THE NSPACE PARAMETER.
      NSPACE=1
      CALL FTGABC(TFIELDS,TFORM,NSPACE,ROWLEN,TBCOL,STATUS)

C  FTPHTB WRITES ALL THE REQUIRED HEADER KEYWORDS WHICH DEFINE THE
C  STRUCTURE OF THE ASCII TABLE. NROWS AND TFIELDS GIVE THE NUMBER OF
C  ROWS AND COLUMNS IN THE TABLE, AND THE TTYPE, TBCOL, TFORM, AND TUNIT
C  ARRAYS GIVE THE COLUMN NAME, STARTING POSITION, FORMAT, AND UNITS,
C  RESPECTIVELY OF EACH COLUMN. THE VALUES OF THE ROWLEN AND TBCOL PARAMETERS
C  WERE PREVIOUSLY CALCULATED BY THE FTGABC ROUTINE.
      CALL FTPHTB(UNIT,ROWLEN,NROWS,TFIELDS,TTYPE,TBCOL,TFORM,TUNIT,
     &            EXTNAME,STATUS)

C  WRITE NAMES TO THE FIRST COLUMN, DIAMETERS TO 2ND COL., AND DENSITY TO 3RD
C  FTPCLS WRITES THE STRING VALUES TO THE NAME COLUMN (COLUMN 1) OF THE
C  TABLE.  THE FTPCLJ AND FTPCLE ROUTINES WRITE THE DIAMETER (INTEGER) AND
C  DENSITY (REAL) VALUE TO THE 2ND AND 3RD COLUMNS.  THE FITSIO ROUTINES
C  ARE COLUMN ORIENTED, SO IT IS USUALLY EASIER TO READ OR WRITE DATA IN A
C  TABLE IN A COLUMN BY COLUMN ORDER RATHER THAN ROW BY ROW.
      FROW=1
      FELEM=1
      COLNUM=1
      CALL FTPCLS(UNIT,COLNUM,FROW,FELEM,NROWS,NAME,STATUS)
      COLNUM=2
      CALL FTPCLJ(UNIT,COLNUM,FROW,FELEM,NROWS,DIAMETER,STATUS)  
      COLNUM=3
      CALL FTPCLE(UNIT,COLNUM,FROW,FELEM,NROWS,DENSITY,STATUS)  

C  THE FITS FILE MUST ALWAYS BE CLOSED BEFORE EXITING THE PROGRAM. 
C  ANY UNIT NUMBERS ALLOCATED WITH FTGIOU MUST BE FREED WITH FTFIOU.
      CALL FTCLOS(UNIT, STATUS)
      CALL FTFIOU(UNIT, STATUS)

C  CHECK FOR ANY ERROR, AND IF SO PRINT OUT ERROR MESSAGES.
C  THE PRINTERROR SUBROUTINE IS LISTED NEAR THE END OF THIS FILE.
      IF (STATUS .GT. 0)CALL PRINTERROR(STATUS)
      END
