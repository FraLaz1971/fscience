C *************************************************************************
      SUBROUTINE WRITEBINTABLE

C  THIS ROUTINE CREATES A FITS BINARY TABLE, OR BINTABLE, CONTAINING
C  3 COLUMNS AND 6 ROWS.  THIS ROUTINE IS NEARLY IDENTICAL TO THE
C  PREVIOUS WRITEASCII ROUTINE, EXCEPT THAT THE CALL TO FTGABC IS NOT
C  NEEDED, AND FTPHBN IS CALLED RATHER THAN FTPHTB TO WRITE THE
C  REQUIRED HEADER KEYWORDS.

      INTEGER STATUS,UNIT,READWRITE,BLOCKSIZE,HDUTYPE,TFIELDS,NROWS
      INTEGER VARIDAT,DIAMETER(6), COLNUM,FROW,FELEM
      REAL DENSITY(6)
      CHARACTER FILENAME*40,EXTNAME*16
      CHARACTER*16 TTYPE(3),TFORM(3),TUNIT(3),NAME(6)
      DATA TTYPE/'PLANET','DIAMETER','DENSITY'/
      DATA TFORM/'8A','1J','1E'/
      DATA TUNIT/' ','KM','G/CM'/
      DATA NAME/'MERCURY','VENUS','EARTH','MARS','JUPITER','SATURN'/
      DATA DIAMETER/4880,12112,12742,6800,143000,121000/
      DATA DENSITY/5.1,5.3,5.52,3.94,1.33,0.69/

C  THE STATUS PARAMETER MUST ALWAYS BE INITIALIZED.
      STATUS=0

C  NAME OF THE FITS FILE TO APPEND THE ASCII TABLE TO:
      FILENAME='ATESTFILEZ.FITS'

C  GET AN UNUSED LOGICAL UNIT NUMBER TO USE TO OPEN THE FITS FILE.
      CALL FTGIOU(UNIT,STATUS)

C  OPEN THE FITS FILE, WITH WRITE ACCESS.
      READWRITE=1
      CALL FTOPEN(UNIT,FILENAME,READWRITE,BLOCKSIZE,STATUS)

C  MOVE TO THE LAST (2ND) HDU IN THE FILE (THE ASCII TABLE).
      CALL FTMAHD(UNIT,2,HDUTYPE,STATUS)

C  APPEND/CREATE A NEW EMPTY HDU ONTO THE END OF THE FILE AND MOVE TO IT.
      CALL FTCRHD(UNIT,STATUS)

C  DEFINE PARAMETERS FOR THE BINARY TABLE (SEE THE ABOVE DATA STATEMENTS)
      TFIELDS=3
      NROWS=6
      EXTNAME='PLANETS_BINARY'
      VARIDAT=0
      
C  FTPHBN WRITES ALL THE REQUIRED HEADER KEYWORDS WHICH DEFINE THE
C  STRUCTURE OF THE BINARY TABLE. NROWS AND TFIELDS GIVES THE NUMBER OF
C  ROWS AND COLUMNS IN THE TABLE, AND THE TTYPE, TFORM, AND TUNIT ARRAYS
C  GIVE THE COLUMN NAME, FORMAT, AND UNITS, RESPECTIVELY OF EACH COLUMN.
      CALL FTPHBN(UNIT,NROWS,TFIELDS,TTYPE,TFORM,TUNIT,
     &            EXTNAME,VARIDAT,STATUS)

C  WRITE NAMES TO THE FIRST COLUMN, DIAMETERS TO 2ND COL., AND DENSITY TO 3RD
C  FTPCLS WRITES THE STRING VALUES TO THE NAME COLUMN (COLUMN 1) OF THE
C  TABLE.  THE FTPCLJ AND FTPCLE ROUTINES WRITE THE DIAMETER (INTEGER) AND
C  DENSITY (REAL) VALUE TO THE 2ND AND 3RD COLUMNS.  THE FITSIO ROUTINES
C  ARE COLUMN ORIENTED, SO IT IS USUALLY EASIER TO READ OR WRITE DATA IN A
C  TABLE IN A COLUMN BY COLUMN ORDER RATHER THAN ROW BY ROW.  NOTE THAT
C  THE IDENTICAL SUBROUTINE CALLS ARE USED TO WRITE TO EITHER ASCII OR
C  BINARY FITS TABLES.
      FROW=1
      FELEM=1
      COLNUM=1
      CALL FTPCLS(UNIT,COLNUM,FROW,FELEM,NROWS,NAME,STATUS)
      COLNUM=2
      CALL FTPCLJ(UNIT,COLNUM,FROW,FELEM,NROWS,DIAMETER,STATUS)  
      COLNUM=3
      CALL FTPCLE(UNIT,COLNUM,FROW,FELEM,NROWS,DENSITY,STATUS)  

C  THE FITS FILE MUST ALWAYS BE CLOSED BEFORE EXITING THE PROGRAM. 
C  ANY UNIT NUMBERS ALLOCATED WITH FTGIOU MUST BE FREED WITH FTFIOU.
      CALL FTCLOS(UNIT, STATUS)
      CALL FTFIOU(UNIT, STATUS)

C  CHECK FOR ANY ERROR, AND IF SO PRINT OUT ERROR MESSAGES.
C  THE PRINTERROR SUBROUTINE IS LISTED NEAR THE END OF THIS FILE.
      IF (STATUS .GT. 0)CALL PRINTERROR(STATUS)
      END
