C *************************************************************************
      SUBROUTINE READTABLE

C  READ AND PRINT DATA VALUES FROM AN ASCII OR BINARY TABLE
C  THIS EXAMPLE READS AND PRINTS OUT ALL THE DATA IN THE ASCII AND
C  THE BINARY TABLES THAT WERE PREVIOUSLY CREATED BY WRITEASCII AND
C  WRITEBINTABLE.  NOTE THAT THE EXACT SAME FITSIO ROUTINES ARE
C  USED TO READ BOTH TYPES OF TABLES.

      INTEGER STATUS,UNIT,READWRITE,BLOCKSIZE,HDUTYPE,NTABLE
      INTEGER FELEM,NELEMS,NULLJ,DIAMETER,NFOUND,IROW,COLNUM
      REAL NULLE,DENSITY
      CHARACTER FILENAME*40,NULLSTR*1,NAME*8,TTYPE(3)*10
      LOGICAL ANYNULL

C  THE STATUS PARAMETER MUST ALWAYS BE INITIALIZED.
      STATUS=0

C  GET AN UNUSED LOGICAL UNIT NUMBER TO USE TO OPEN THE FITS FILE.
      CALL FTGIOU(UNIT,STATUS)

C  OPEN THE FITS FILE PREVIOUSLY CREATED BY WRITEIMAGE
      FILENAME='ATESTFILEZ.FITS'
      READWRITE=0
      CALL FTOPEN(UNIT,FILENAME,READWRITE,BLOCKSIZE,STATUS)

C  LOOP TWICE, FIRST READING THE ASCII TABLE, THEN THE BINARY TABLE
      DO NTABLE=2,3

C  MOVE TO THE NEXT EXTENSION
          CALL FTMAHD(UNIT,NTABLE,HDUTYPE,STATUS)

          PRINT *,' '
          IF (HDUTYPE .EQ. 1)THEN
              PRINT *,'READING ASCII TABLE IN HDU ',NTABLE
          ELSE IF (HDUTYPE .EQ. 2)THEN
              PRINT *,'READING BINARY TABLE IN HDU ',NTABLE
          END IF

C  READ THE TTYPEN KEYWORDS, WHICH GIVE THE NAMES OF THE COLUMNS
          CALL FTGKNS(UNIT,'TTYPE',1,3,TTYPE,NFOUND,STATUS)
          WRITE(*,2000)TTYPE
2000      FORMAT(2X,"ROW   ",3A10)

C  READ THE DATA, ONE ROW AT A TIME, AND PRINT THEM OUT
          FELEM=1
          NELEMS=1
          NULLSTR=' '
          NULLJ=0
          NULLE=0.
          DO IROW=1,6
C             FTGCVS READS THE NAMES FROM THE FIRST COLUMN OF THE TABLE.
              COLNUM=1
              CALL FTGCVS(UNIT,COLNUM,IROW,FELEM,NELEMS,NULLSTR,NAME,
     &                    ANYNULL,STATUS)

C             FTGCVJ READS THE DIAMETER VALUES FROM THE SECOND COLUMN.
              COLNUM=2
              CALL FTGCVJ(UNIT,COLNUM,IROW,FELEM,NELEMS,NULLJ,DIAMETER,
     &                    ANYNULL,STATUS)

C             FTGCVE READS THE DENSITY VALUES FROM THE THIRD COLUMN.
              COLNUM=3
              CALL FTGCVE(UNIT,COLNUM,IROW,FELEM,NELEMS,NULLE,DENSITY,
     &                    ANYNULL,STATUS)
              WRITE(*,2001)IROW,NAME,DIAMETER,DENSITY
2001          FORMAT(I5,A10,I10,F10.2)
          END DO
      END DO

C  THE FITS FILE MUST ALWAYS BE CLOSED BEFORE EXITING THE PROGRAM. 
C  ANY UNIT NUMBERS ALLOCATED WITH FTGIOU MUST BE FREED WITH FTFIOU.
      CALL FTCLOS(UNIT, STATUS)
      CALL FTFIOU(UNIT, STATUS)

C  CHECK FOR ANY ERROR, AND IF SO PRINT OUT ERROR MESSAGES.
C  THE PRINTERROR SUBROUTINE IS LISTED NEAR THE END OF THIS FILE.
      IF (STATUS .GT. 0)CALL PRINTERROR(STATUS)
      END
